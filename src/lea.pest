source = {
  SOI ~
  (module | structDecl | functionDecl)* ~
  EOI
}

module = { "module" ~ ident ~ ";" }
structMember = { ident ~ ":" ~ type }
structDecl = { "struct" ~ ident ~ "{" ~ (structMember ~ ("," ~ structMember)*)? ~ ","? ~ "}" }
functionDecl = { "fn" ~ ident ~ params ~ ("->" ~ type)? ~ block }

param = { ident ~ ":" ~ type }
params = _{ "(" ~ (param ~ ("," ~ param)*)? ~ ")" }

block = { "{" ~ (stmt*)? ~ "}" }

stmt = _{ (expr | varDecl | returnStmt) ~ ";" }
varDecl = { "let" ~ ident ~ (":" ~ type)? ~ "=" ~ (lit | ident) }
returnStmt = { "return" ~ (ident | lit)? }

expr = _{ callExpr }
callExpr = { ident ~ "(" ~ (arg ~ ("," ~ arg)*)? ~ ")" }

arg = _{ (ident | lit) }

lit = _{ numLit | strLit | charLit | boolLit }
numLit = { (ASCII_DIGIT | ".")+ }
strLit = ${ "\"" ~ inner ~ "\"" }
charLit = { "'" ~ char ~ "'" }
boolLit = { "true" | "false" }

char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
inner = @{ char* }

ident = @{ (ASCII_ALPHANUMERIC | "_")+ }

primitive = { "i8" | "i16" | "i32" | "i64" | "char" | "bool" }
object = { (ASCII_ALPHANUMERIC)+ }
array = { "[]" }
type = _{ (primitive | object) ~ array? }

WHITESPACE = _{ " " | "\t" | "\n" }
